apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.8

// Folder where the plugins are built
ext.pluginsDir = "${rootProject.buildDir}/plugins"

configurations {
  provided
  compile.extendsFrom(provided)
}

repositories {
  mavenCentral()
  mavenLocal()
  maven {
    url "https://artifactory.lucidworks.com/artifactory/public-artifacts/"
  }
}

dependencies {
  compile group: 'com.google.apis', name: 'google-api-services-youtube', version: 'v3-rev222-1.25.0'
  compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
  compile group: 'com.google.http-client', name: 'google-http-client', version: '1.35.0'
  compile 'io.opencensus:opencensus-contrib-http-util:0.24.0'
  compile group: 'io.opencensus', name: 'opencensus-impl', version: '0.24.0'



  provided("com.lucidworks-connector.sdk:connector-plugin-sdk:${connectorsSDKVersion}")
  provided "org.slf4j:slf4j-api:${slf4jVersion}"
  testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
  archiveBaseName = "${project.name}"
}

task plugin(type: Jar) {
  archiveFileName = "${project.name}" + ".zip"
  manifest {
    attributes 'Plugin-Class': "${pluginClass}",
            'Plugin-Type': "connector",
            'Plugin-Id': "${pluginId}",
            'Plugin-Version': "${version}",
            'Plugin-Provider': "${pluginProvider}",
            'Plugin-Connectors-SDK-Version': "${connectorsSDKVersion}"
  }
  into('lib') {
    from configurations.compile - configurations.provided
    // Copy the plugin
    from jar
  }
  extension('zip')
}

task assemblePlugin(type: Copy) {
  from plugin
  into pluginsDir
}